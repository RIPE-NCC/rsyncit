plugins {
    id 'java'
    id 'io.freefair.lombok' version '9.1.0'
    id 'com.gorylenko.gradle-git-properties' version '2.5.4'
    id 'org.sonarqube' version '3.5.0.2730'
    id 'com.google.cloud.tools.jib' version '3.5.2'
    id 'application'
}

apply plugin: 'java'

group = 'net.ripe.rpki'
version = '0.5.0-SNAPSHOT'

application {
    mainClass = 'net.ripe.rpki.rsyncit.RsyncitApplication'
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://nexus.ripe.net/nexus/content/repositories/releases/')
    }
}

dependencies {
    // Lightweight HTTP server
    implementation 'io.javalin:javalin:6.5.0'

    // Reactor Netty for HTTP client (standalone, without Spring)
    implementation 'io.projectreactor.netty:reactor-netty-http:1.2.5'

    // Jackson for JSON serialization
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3'

    // Metrics
    implementation 'io.micrometer:micrometer-core:1.15.0'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.15.0'

    // Cron scheduler
    implementation 'org.quartz-scheduler:quartz:2.5.0'

    implementation 'com.google.guava:guava:33.5.0-jre'

    implementation "net.ripe.rpki:rpki-commons:1.41"

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'ch.qos.logback:logback-classic:1.5.18'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.1'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

dependencyLocking {
    lockAllConfigurations()
}

tasks.named('test') {
    useJUnitPlatform()
}

// Create a fat jar with all dependencies
jar {
    manifest {
        attributes 'Main-Class': 'net.ripe.rpki.rsyncit.RsyncitApplication'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
